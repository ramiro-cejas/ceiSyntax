<Inicial> ::= <ListaClases> "$"
<ListaClases> ::= <Clase> <ListaClases> | ε
<Clase> ::= <ClaseConcreta> | <InterfaceConcreta>
<ClaseConcreta> ::= "class" "idClass" <GenericoConID> <HerenciaOpcional> "{" <ListaMiembros> "}"
<InterfaceConcreta> ::= "interface" "idInterface" <GenericoConID> <ExtiendeOpcional> "{" <ListaEncabezados> "}"
<HerenciaOpcional> ::= <HeredaDe> | <ImplementaA> | ε
<HeredaDe> ::= "extends" "idClass" <GenericoOpcional>
<ImplementaA> ::= "implements" "idInterface" <GenericoOpcional>
<ExtiendeOpcional> ::= "extends" "idClass" <GenericoOpcional> | ε
<ListaMiembros> ::= <Miembro> <ListaMiembros> | ε
<ListaEncabezados> ::= <EncabezadoMetodo> <ListaEncabezados> | ε
<Miembro> ::= <Parte1Miembro> <MetodoOAtributo> | <Constructor>
<MetodoOAtributo> ::= <PosiblesExtrasAtributos> <AsignacionOpcionalDeExpresion> ";" | <ArgsFormales> <Bloque>
<PosiblesExtrasAtributos> ::= "," "idMetVar" <PosiblesExtrasAtributos> | ε
<EncabezadoMetodo> ::= <Parte1Miembro> <ArgsFormales> ";"
<Parte1Miembro> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar"
<Constructor> ::= "public" "idClass" <GenericoOpcional> <ArgsFormales> <Bloque>
<TipoMiembro> ::= <Tipo> | "void"
<Tipo> ::= <TipoPrimitivo> | "idClass" <GenericoOpcional>
<TipoPrimitivo> ::= "boolean" | "char" | "int" | "float"
<EstaticoOpcional> ::= "static" | ε
<ArgsFormales> ::= "(" <ListaArgsFormalesOpcional> ")"
<ListaArgsFormalesOpcional> ::= <ArgFormal> <ListaArgsFormales> | ε
<ListaArgsFormales> ::= "," <ArgFormal> <ListaArgsFormales> | ε
<ArgFormal> ::= <Tipo> "idMetVar"
<Bloque> ::= "{" <ListaSentencias> "}"
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | ε
<Sentencia> ::= ";" | <AsignacionOLlamada> ";" | <VarLocal> ";" | <Retorno> ";" | <SentenciaIf> | <SentenciaWhile> | <Bloque>
<AsignacionOLlamada> ::= <Expresion>
<VarLocal> ::= "var" "idMetVar" "=" <ExpresionCompuesta>
<Retorno> ::= "return" <ExpresionOpcional>
<ExpresionOpcional> ::= <Expresion> | ε
<SentenciaIf> ::= "if" "(" <Expresion> ")" <Sentencia> <ElseOpcional>
<ElseOpcional> ::= "else" <Sentencia> | ε
<SentenciaWhile> ::= "while" "(" <Expresion> ")" <Sentencia>
<Expresion> ::= <ExpresionCompuesta> <AsignacionOpcionalDeExpresion>
<AsignacionOpcionalDeExpresion> ::= "=" <Expresion> | ε
<ExpresionCompuesta> ::= <OperadorBinario> <ExpresionBasica> <RecursionExpresionCompuesta> | ε
<RecursionExpresionCompuesta> ::= <OperadorBinario> <ExpresionBasica> <RecursionExpresionCompuesta> | ε
<OperadorBinario> ::= "||" | "&&" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "+" | "-" | "*" | "/" | "%"
<ExpresionBasica> ::= <OperadorUnario> <Operando> | <Operando>
<OperadorUnario> ::= "+" | "-" | "!"
<Operando> ::= <Literal> | <Acceso>
<Literal> ::= "null" | "true" | "false" | "intLiteral" | "charLiteral" | "stringLiteral" | "floatLiteral"
<Acceso> ::= <Primario> <EncadenadoOpcional>
<Primario> ::= <AccesoThis> | <AccesoMetVar> | <AccesoConstructor> | <AccesoMetodoEstatico> | <ExpresionParentizada>
<AccesoThis> ::= "this"
<AccesoMetVar> ::= "idMetVar" <ContinuacionMetodo>
<ContinuacionMetodo> ::= <ArgsActuales> | ε
<AccesoConstructor> ::= "new" "idClass" <GenericoOpcional> <ArgsActuales>
<ExpresionParentizada> ::= "(" <Expresion> ")"
<AccesoMetodoEstatico> ::= "idClass" <GenericoOpcional> "." "idMetVar" <ArgsActuales>
<ArgsActuales> ::= "(" <ListaExpsOpcional> ")"
<ListaExpsOpcional> ::= <Expresion> <ListaExps> | ε
<ListaExps> ::= "," <Expresion> <ListaExps> | ε
<EncadenadoOpcional> ::= <PuntoYMetVar> <ArgsOpcionales> <EncadenadoOpcional> | ε
<PuntoYMetVar> ::= "." "idMetVar"
<ArgsOpcionales> ::= <ArgsActuales> | ε
<GenericoOpcional> ::= "<" <ClaseOpcional> ">" | ε
<ClaseOpcional> ::= "idClass" <ListaDeClasesOpcionales> | ε
<ListaDeClasesOpcionales> ::= "," "idClass" <ListaDeClasesOpcionales> | ε
<GenericoConID> ::= "<" "idClass" <ListaDeClasesOpcionales> ">" | ε