<Inicial> ::= <ListaClases> "$"
<ListaClases> ::= <Clase> <ListaClases> | ε
<Clase> ::= <ClaseConcreta> | <Interface>
<ClaseConcreta> ::= "class" "idClase" <HerenciaOpcional> "{" <ListaMiembros> "}"
<Interface> ::= "interface" "idClase" <ExtiendeOpcional> "{" <ListaEncabezados> "}"
<HerenciaOpcional> ::= <HeredaDe> | <ImplementaA> | ε
<HeredaDe> ::= "extends" "idClase"
<ImplementaA> ::= "implements" "idClase"
<ExtiendeOpcional> ::= "extends" "idClase" | ε
<ListaMiembros> ::= <Miembro> <ListaMiembros> | ε
<ListaEncabezados> ::= <EncabezadoMetodo> <ListaEncabezados> | ε
<Miembro> ::= <Atributo> | <Metodo> | <Constructor>
<Atributo> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar" ";"
<Metodo> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar" <ArgsFormales> <Bloque>
<EncabezadoMetodo> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar" <ArgsFormales> ";"
<Constructor> ::= "public" "idClase" <ArgsFormales> <Bloque>
<TipoMiembro> ::= <Tipo> | "void"
<Tipo> ::= <TipoPrimitivo> | "idClase"
<TipoPrimitivo> ::= "boolean" | "char" | "int"
<EstaticoOpcional> ::= "static" | ε
<ArgsFormales> ::= "(" <ListaArgsFormalesOpcional> ")"
<ListaArgsFormalesOpcional> ::= <ListaArgsFormales> | ε
<ListaArgsFormales> ::= <ArgFormal>
<ListaArgsFormales> ::= <ArgFormal> , <ListaArgsFormales>
<ArgFormal> ::= <Tipo> "idMetVar"
<Bloque> ::= "{" <ListaSentencias> "}"
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | ε
<Sentencia> ::= ";"
<Sentencia> ::= <Asignacion> ";"
<Sentencia> ::= <Llamada> ";"
<Sentencia> ::= <VarLocal> ";"
<Sentencia> ::= <Return> ";"
<Sentencia> ::= <If>
<Sentencia> ::= <While>
<Sentencia> ::= <Bloque>
<Asignacion> ::= <Expresion>
<Llamada> ::= <Expresion>
<VarLocal> ::= "var" "idMetVar" "=" <ExpresionCompuesta>
<Return> ::= "return" <ExpresionOpcional>
<ExpresionOpcional> ::= <Expresion> | ε
<If> ::= "if" "(" <Expresion> ")" <Sentencia>
<If> ::= "if" "(" <Expresion> ")" <Sentencia> "else" <Sentencia>
<While> ::= "while" "(" <Expresion> ")" <Sentencia>
<Expresion> ::= <ExpresionCompuesta> = <Expresion>
<Expresion> ::= <ExpresionCompuesta>

<ExpresionCompuesta> ::= <OperadorBinario> <ExpresionBasica> <RecursionExpresionCompuesta> | ε
<RecursionExpresionCompuesta> ::= <OperadorBinario> <ExpresionBasica> <RecursionExpresionCompuesta> | ε

<OperadorBinario> ::= "||" | "&&" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "+" | "-" | "*" | "/" | "%"
<ExpresionBasica> ::= <OperadorUnario> <Operando>
<ExpresionBasica> ::= <Operando>
<OperadorUnario> ::= "+" | "-" | "!"
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= "null" | "true" | "false" | "intLiteral" | "charLiteral" | "stringLiteral"
<Acceso> ::= <Primario> <EncadenadoOpcional>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoVar>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoMetodo>
<Primario> ::= <AccesoMetodoEstatico>
<Primario> ::= <ExpresionParentizada>
<AccesoThis> ::= "this"
<AccesoVar> ::= "idMetVar"
<AccesoConstructor> ::= "new" "idClase" <ArgsActuales>
<ExpresionParentizada> ::= "(" <Expresion> ")"
<AccesoMetodo> ::= "idMetVar" <ArgsActuales>
<AccesoMetodoEstatico> ::= "idClase" "." "idMetVar" <ArgsActuales>
<ArgsActuales> ::= "(" <ListaExpsOpcional> ")"
<ListaExpsOpcional> ::= <ListaExps> | ε
<ListaExps> ::= <Expresion>
<ListaExps> ::= <Expresion> "," <ListaExps>
<EncadenadoOpcional> ::= <VarEncadenada> | <MetodoEncadenado> | ε
<VarEncadenada> ::= "." "idMetVar" <EncadenadoOpcional>
<MetodoEncadenado> ::= "." "idMetVar" <ArgsActuales> <EncadenadoOpcional>