<Inicial> ::= <ListaClases> "$"
<ListaClases> ::= <Clase> <ListaClases> | #
<Clase> ::= <ClaseConcreta> | <Interface>
<ClaseConcreta> ::= "class" "idClase" <HerenciaOpcional> "{" <ListaMiembros> "}"
<Interface> ::= "interface" "idClase" <ExtiendeOpcional> "{" <ListaEncabezados> "}"
<HerenciaOpcional> ::= <HeredaDe> | <ImplementaA> | #
<HeredaDe> ::= "extends" "idClase"
<ImplementaA> ::= "implements" "idClase"
<ExtiendeOpcional> ::= "extends" "idClase" | #
<ListaMiembros> ::= <Miembro> <ListaMiembros> | #
<ListaEncabezados> ::= <EncabezadoMetodo> <ListaEncabezados> | #

<Miembro> ::= <Parte1Miembro> <MetodoOAtributo> | <Constructor>

<MetodoOAtributo> ::= ";" | <ArgsFormales> <Bloque>

<EncabezadoMetodo> ::= <Parte1Miembro> <ArgsFormales> ";"

<Parte1Miembro> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar"

<Constructor> ::= "public" "idClase" <ArgsFormales> <Bloque>
<TipoMiembro> ::= <Tipo> | "void"
<Tipo> ::= <TipoPrimitivo> | "idClase"
<TipoPrimitivo> ::= "boolean" | "char" | "int" | "float"
<EstaticoOpcional> ::= "static" | #
<ArgsFormales> ::= "(" <ListaArgsFormalesOpcional> ")"
<ListaArgsFormalesOpcional> ::= <ArgFormal> <ListaArgsFormales> | #

<ListaArgsFormales> ::= "," <ArgFormal> <ListaArgsFormales> | #

<ArgFormal> ::= <Tipo> "idMetVar"
<Bloque> ::= "{" <ListaSentencias> "}"
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | #

<Sentencia> ::= ";"
<Sentencia> ::= <AsignacionOLlamada> ";"
<Sentencia> ::= <VarLocal> ";"
<Sentencia> ::= <Return> ";"
<Sentencia> ::= <If>
<Sentencia> ::= <While>
<Sentencia> ::= <Bloque>

<AsignacionOLlamada> ::= <Expresion>
<VarLocal> ::= "var" "idMetVar" = <ExpresionCompuesta>
<Return> ::= "return" <ExpresionOpcional>
<ExpresionOpcional> ::= <Expresion> | #

<If> ::= <ObligatoryIf> <Else>
<ObligatoryIf> ::= "if" "(" <Expresion> ")" <Sentencia>
<Else> ::= "else" <Sentencia> | #

<While> ::= "while" "(" <Expresion> ")" <Sentencia>
<Expresion> ::= <ExpresionCompuesta> <AsignacionOpcionalDeExpresion>
<AsignacionOpcionalDeExpresion> ::= "=" <Expresion> | #

/<ExpresionCompuesta> ::= <ExpresionCompuesta> <OperadorBinario> <ExpresionBasica>
<ExpresionCompuesta> ::= <ExpresionBasica>/
<ExpresionCompuesta> ::= <ExpresionBasica> <R>
<R> ::= <OperadorBinario> <ExpresionBasica> <R> | #

<OperadorBinario> ::= "||" | "&&" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "+" | "-" | "*" | "/" | "%"
<ExpresionBasica> ::= <OperadorUnario> <Operando>
<ExpresionBasica> ::= <Operando>
<OperadorUnario> ::= "+" | "-" | "!"
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= "null" | "true" | "false" | "intLiteral" | "charLiteral" | "stringLiteral"
<Acceso> ::= <Primario> <EncadenadoOpcional>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoMetVar>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoMetodoEstatico>
<Primario> ::= <ExpresionParentizada>
<AccesoThis> ::= "this"

<AccesoMetVar> ::= "idMetVar" <ContinuacionMetodo>
<ContinuacionMetodo> ::= <ArgsActuales> | #

<AccesoConstructor> ::= "new" "idClase" <ArgsActuales>
<ExpresionParentizada> ::= "(" <Expresion> ")"
<AccesoMetodoEstatico> ::= "idClase" "." "idMetVar" <ArgsActuales>
<ArgsActuales> ::= "(" <ListaExpsOpcional> ")"

<ListaExpsOpcional> ::= <Expresion> <ListaExps> | #
<ListaExps> ::= "," <ListaExps> | #

<EncadenadoOpcional> ::= <PuntoYMetVar> <ArgsOpcionales> <EncadenadoOpcional>| #

<PuntoYMetVar> ::= "." "idMetVar"
<ArgsOpcionales> ::= <ArgsActuales> | #