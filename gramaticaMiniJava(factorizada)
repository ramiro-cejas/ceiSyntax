<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <ListaClases> | #
<Clase> ::= <ClaseConcreta> | <Interface>
<ClaseConcreta> ::= "class" <IdClasePosibleGenerico> <HerenciaOpcional> "{" <ListaMiembros> "}"
<Interface> ::= "interface" <IdClasePosibleGenerico> <ExtiendeOpcional> "{" <ListaEncabezados> "}"
<HerenciaOpcional> ::= <HeredaDe> | <ImplementaA> | #
<HeredaDe> ::= "extends" <IdClasePosibleGenerico>
<ImplementaA> ::= "implements" <IdClasePosibleGenerico>
<ExtiendeOpcional> ::= "extends" <IdClasePosibleGenerico> | #
<ListaMiembros> ::= <Miembro> <ListaMiembros> | #
<ListaEncabezados> ::= <EncabezadoMetodo> <ListaEncabezados> | #

<Miembro> ::= "public" <MiembroPosiblePublico> | "private" <MiembroConVisibilidad> | <MiembroConVisibilidad>
<MiembroPosiblePublico> ::= <MiembroConVisibilidad> | <Constructor>
<MiembroConVisibilidad> ::= <Atributo> | <Metodo>

/El ListadoDeAtributos son mas "idMetVar" o bien vacio/
<Atributo> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar" <ListadoDeAtributos> <PosibleAsignacion> ";"

<Metodo> ::= <EstaticoOpcional> <TipoMiembro> "idMetVar" <ArgsFormales> <Bloque>
<EncabezadoMetodo> ::= <EstaticoOpcional> <TipoMetodo> "idMetVar" <ArgsFormales> ";"
<Constructor> ::= <IdClasePosibleGenerico> <ArgsFormales> <Bloque>
<TipoMiembro> ::= <Tipo> | "void"
<Tipo> ::= <TipoPrimitivo> | <IdClasePosibleGenerico>
<TipoPrimitivo> ::= "boolean" | "char" | "int" | "float"
<EstaticoOpcional> ::= "static" | #
<ArgsFormales> ::= "(" <ListaArgsFormalesOpcional> ")"
<ListaArgsFormalesOpcional> ::= <ListaArgsFormales> | #
<ListaArgsFormales> ::= <ArgFormal>
<ListaArgsFormales> ::= <ArgFormal> "," <ListaArgsFormales>
<ArgFormal> ::= <Tipo> "idMetVar"
<Bloque> ::= "{" <ListaSentencias> "}"
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | #
<Sentencia> ::= ";"
<Sentencia> ::= <Asignacion> ";"
<Sentencia> ::= <Llamada> ";"
<Sentencia> ::= <VarLocal> ";"
<Sentencia> ::= <Return> ";"
<Sentencia> ::= <If>
<Sentencia> ::= <While>
<Sentencia> ::= <Bloque>
<Asignacion> ::= <Expresion>
<Llamada> ::= <Expresion>
<VarLocal> ::= "var" "idMetVar" "=" <ExpresionCompuesta>
<Return> ::= "return" <ExpresionOpcional>
<ExpresionOpcional> ::= <Expresion> | #
<If> ::= "if" "(" <Expresion> ")" <Sentencia>
<If> ::= "if" "(" <Expresion> ")" <Sentencia> "else" <Sentencia>
<While> ::= "while" "(" <Expresion> ")" <Sentencia>
<Expresion> ::= <ExpresionCompuesta> "=" <Expresion>
<Expresion> ::= <ExpresionCompuesta>

/Se va a corregir la recursi√≥n a izquierda/
/<ExpresionCompuesta> ::= <ExpresionCompuesta> <OperadorBinario> <ExpresionBasica>
<ExpresionCompuesta> ::= <ExpresionBasica>/
<ExpresionCompuesta> ::= <ExpresionBasica> <RecursionExpresionCompuesta>
<RecursionExpresionCompuesta> ::= <OperadorBinario> <ExpresionBasica> <RecursionExpresionCompuesta>

<OperadorBinario> ::= "||" | "&&" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "+" | "-" | "*" | "/" | "%"
<ExpresionBasica> ::= <OperadorUnario> <Operando>
<ExpresionBasica> ::= <Operando>
<OperadorUnario> ::= "+" | "-" | "!"
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= "null" | "true" | "false" | "intLiteral" | "charLiteral" | "stringLiteral" | "floatLiteral"
<Acceso> ::= <Primario> <EncadenadoOpcional>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoVar>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoMetodo>
<Primario> ::= <AccesoMetodoEstatico>
<Primario> ::= <ExpresionParentizada>
<AccesoThis> ::= "this"
<AccesoVar> ::= "idMetVar"
<AccesoConstructor> ::= "new" <IdClasePosibleGenerico> <ArgsActuales>
<ExpresionParentizada> ::= "(" <Expresion> ")"
<AccesoMetodo> ::= "idMetVar" <ArgsActuales>
<AccesoMetodoEstatico> ::= <IdClasePosibleGenerico> "." "idMetVar" <ArgsActuales>
<ArgsActuales> ::= "(" <ListaExpsOpcional> ")"
<ListaExpsOpcional> ::= <ListaExps> | #
<ListaExps> ::= <Expresion>
<ListaExps> ::= <Expresion> "," <ListaExps>
<EncadenadoOpcional> ::= <VarEncadenada> | <MetodoEncadenado> | #
<VarEncadenada> ::= "." "idMetVar" <EncadenadoOpcional>
<MetodoEncadenado> ::= "." "idMetVar" <ArgsActuales> <EncadenadoOpcional>

/ ############# VarClasica ############# /
<Sentencia> ::= <VarClasica> ";"
<VarClasica> ::= <Tipo> "idMetVar" <ListadoDeAtributos> "=" <ExpresionCompuesta>
<ListadoDeAtributos> ::= "," "idMetVar" <ListadoDeAtributos> | #

/ ############# idClase con genericidad ############# /
<IdClasePosibleGenerico> ::= "idClase" <Generico>
/ aca se puede reemplazar "idClase" por <Tipo> y podemos tenemos genericidad anidada /
<Generico> ::= "<" "idClase" ">" | #

/ Para los floats se agrego el floatLiteral a los literales y ademas se agrego float a los tipos primitivos /